import nu.studer.gradle.jooq.JooqConfiguration
import nu.studer.gradle.jooq.JooqTask
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jooq.meta.jaxb.*
import org.jooq.meta.jaxb.Target
import org.springframework.boot.gradle.tasks.run.BootRun


group = "{{project.name}}"
version = "{{service.version}}"

val jooqVersion: String by project
val jooqSchemaPackage: String by project

plugins {
    val kotlinVersion = "1.3.10"
    val springbootVersion = "2.1.0.RELEASE"
    kotlin("jvm") // version kotlinVersion
    id("org.jetbrains.kotlin.plugin.spring") version kotlinVersion
    id("io.spring.dependency-management") version "1.0.6.RELEASE"
    id("org.springframework.boot") version springbootVersion
    id("nu.studer.jooq") version "3.0.1"
}

repositories {
  jcenter()
}

dependencies {
    val kotlinCoroutineVersion = "1.0.1"

    compile(kotlin("stdlib-jdk8"))
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutineVersion")
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinCoroutineVersion")
    compile("org.springframework.boot:spring-boot-starter-webflux")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-jooq")
    compile("com.fasterxml.jackson.module:jackson-module-kotlin")
    compile("com.graphql-java-kickstart:graphql-java-tools:5.3.5")
	compile("com.graphql-java:graphql-java:11.0")
    compile("com.graphql-java:java-dataloader:2.2.1")
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.postgresql:postgresql:42.2.5")
    compile("org.jooq:jooq")

    testCompile("junit:junit")
    testCompile("org.springframework.boot:spring-boot-starter-test")

    jooqRuntime("org.postgresql:postgresql")
    jooqRuntime("org.jooq:jooq:$jooqVersion")
    jooqRuntime("org.jooq:jooq-meta:$jooqVersion")
    jooqRuntime("org.jooq:jooq-codegen:$jooqVersion")
}

tasks {
    withType<KotlinCompile> {
        kotlinOptions.jvmTarget = "1.8"
    }
    withType<BootRun> {
        jvmArgs = listOf(
            "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005"
        )
    }
}

sourceSets {
    listOf("main", "test").onEach {
        getByName(it).apply {
            java.srcDirs("src/gen/java", "src/gen/kotlin")
            resources.srcDirs("src/gen/resources")
        }
    }
}

val generateJooqDsl by tasks.creating(JooqTask::class.java) {
    jooqClasspath = project.configurations.getByName("jooqRuntime")
    configuration = Configuration().apply {
        jdbc = Jdbc().apply {
            url = System.getenv("DATASOURCE_URL")
            user = System.getenv("DATASOURCE_USER")
            password = System.getenv("DATASOURCE_PASS")
        }
        generator = Generator().apply {
            name = "org.jooq.codegen.DefaultGenerator"
            database = Database().apply {
                name = "org.jooq.meta.postgres.PostgresDatabase"
                inputSchema = "public"
                includes = ".*"
                excludes = ""
            }
            target = Target().apply {
                packageName = jooqSchemaPackage
                directory = "src/gen/java"
            }
        }
    }
}
